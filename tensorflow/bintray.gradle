apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'


ext {
    developerId = 'iglaweb'
    developerName = 'Igor Lashkov'
    developerEmail = 'igor-lashkov@ya.ru'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = 'Apache-2.0'
}

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource

        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }

    jar {
        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
File local = project.rootProject.file('local.properties')
if(local.exists()) {
    properties.load(local.newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayRepoName
        licenses = allLicenses.split(',')
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true
        publicDownloadNumbers = true
        override = true
        version {
            desc = libraryDescription
            name = '0.0.1'
            released  = new Date()
        }
    }
}